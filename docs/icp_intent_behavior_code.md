# 结构化半自然语言编程语法手册

## 概述

本语法规范定义了一种名为 Intent Behavior Code (意图行为描述代码) 的结构化半自然语言编程描述代码，它是ICP - Intent Code Protocol 中极端重要的核心组成，是一种结合自然语言表达与编程结构的设计方法。

它让开发者能够用直观的方式描述程序行为，同时保持代码的结构化和可维护性。这种语法特别适合在AI辅助编程场景下使用。

下文为对IBC的基本语法结构的定义和描述。

## 语法元素详解

### 1. 模块引用 (module)

**用途**：声明文件依赖的外部模块

**语法模板**：

```intent_behavior_code
module 模块名[: 模块来源和用途描述]
```

**规则**：

- 必须出现在文件顶部
- `module` 关键字只在行开头被认为是关键字
- 描述内容说明模块来源（内置库、第三方库等），为可选项
- 模块名必须是合法标识符

**示例**：

```intent_behavior_code
module requests: Python第三方HTTP请求库
module threading: 系统线程库
module utils
```

### 2. 函数定义 (func)

**用途**：定义函数及其执行逻辑

**语法模板**：

```intent_behavior_code
func 函数名(<参数1>[: 参数描述], <参数2>[: 参数描述], ...):
    函数体描述
```

**规则**：

- `func` 关键字只在行开头被认为是关键字
- 参数列表用括号包围，逗号分隔
- 参数描述为可选项，用方括号`[]`表示
- 参数列表允许换行和缩进排列以提升可读性
- 冒号后换行并缩进4个空格描述函数体
- 函数体使用自然语言描述执行步骤

**示例**：

```intent_behavior_code
func 计算订单总价(
    商品列表: 包含价格信息的商品对象数组, 
    折扣率: 0到1之间的小数，表示折扣比例
):
    初始化 总价 = 0
    遍历 商品列表 中的每个 商品:
        总价 = 总价 + 商品.价格
    总价 = 总价 × 折扣率
    返回 总价

func 简单函数(参数1, 参数2):
    执行某些操作
    返回 结果
```

### 3. 类定义 (class)

**用途**：定义类结构，包含成员变量和方法

**语法模板**：

```intent_behavior_code
class 类名([父类名][: 继承方式描述]):
    类体描述
```

**规则**：

- `class` 关键字只在行开头被认为是关键字
- 继承关系在括号中指定，无继承则留空
- 继承描述为可选项，描述继承方式，或者这个继承的功能/作用是什么
- 类体包含成员变量和方法定义
- 使用缩进表示层次结构

**示例**：

```intent_behavior_code
class UserManager(BaseManager: 使用公共基类管理生命周期):
    var users: 用户数据字典
    var dbConnection: 数据库连接对象
    
    func 添加用户(用户名, 密码: 经过哈希处理的密码字符串):
        验证 用户名 和 密码 格式
        创建新用户对象
        将用户保存到数据库
        返回 操作结果

class SimpleClass():
    var 成员变量
    func 方法名():
        方法实现
```

### 4. 对外描述 (description)

**用途**：为函数或类提供简洁的功能说明，主要用于在其他代码引用该符号时向AI模型说明其用途

**语法模板**：

```intent_behavior_code
description: 功能描述文本
```

**规则**：

- `description` 关键字只在行开头被认为是关键字
- 应该在对外可见的符号定义之前出现
- 描述内容尽可能精简地表示符号的对外功能
- 当内容直接跟在冒号后时，不允许换行
- 如需多行显示，必须换行并增加一级缩进
- 这些描述*不会*在生成当前函数或类的代码时提供给AI模型，仅在外部引用或进行依赖分析时使用

**示例**：

```intent_behavior_code
description: 处理用户登录请求，验证凭据并返回认证结果

description:
    这是一个复杂的配置管理系统，具备的功能有
    从多个数据源读取配置信息，合并冲突设置，还提供热重载功能
```

### 5. 意图注释 (@)

**用途**：为函数或类提供额外的设计意图和约束信息，这些信息会在代码生成过程中提供给AI模型参考

**语法模板**：

```intent_behavior_code
@ 注释内容
```

**规则**：

- 仅允许出现在 `func` 和 `class` 定义之前
- 如果与 `description` 同时出现，必须紧贴在目标函数或类之前，在 `description`关键字之后
- 意图注释的内容不允许换行，必须单独占据一行，内容应当精简
- 同一个函数或类前暂时不允许出现多个意图注释
- 只应提供特殊设计决策、性能考虑、约束条件等元信息

**示例**：

```intent_behavior_code
description: 用户认证服务，处理登录和权限验证
@ 线程安全设计，所有公共方法都内置锁机制
class AuthService():
    
    @ 使用bcrypt进行密码哈希，计算成本因子为12
    func 哈希密码(明文密码):
        实现密码哈希逻辑
        返回 哈希结果
```

### 6. 变量声明 (var)

**用途**：显式声明变量

**语法模板**：

```intent_behavior_code
var <变量1>[: 描述1], <变量2>[: 描述2], ...
```

**规则**：

- `var` 关键字只在行开头被认为是关键字
- 变量名必须是合法标识符
- 描述内容为可选项，如提供则不应超过20个汉字

**示例**：

```intent_behavior_code
var userCount: 当前在线用户数量, cacheData: 临时缓存数据
var config
```

### 7. 符号引用 ($)

**用途**：显式且明确地引用已定义的函数、变量、类等符号

**语法模板**：

```intent_behavior_code
$符号分层路径.符号名$
```

**规则**：

- 建议使用点号分隔层级：`$路径.模块.类.方法$`
- 只能在行为步骤描述中使用
- 引用方法时，若涉及参数列表请单独书写，不要包括在 `$symbol$` 中
- 不允许嵌套式引用
- *仅在明确需要时使用*，当前作用域内明确可见的已定义符号优先用行为描述隐式调用

**示例**：

```intent_behavior_code
var maxRetries: 最大重试次数

func 发送请求(请求数据):
    初始化 重试计数 = 0
    // 此处其实完全可以隐式调用，相信大模型的代码能力
    当 重试计数 < $maxRetries$:
        尝试:
            httpClient.post(请求数据)
            返回 成功
        捕获 异常:
            重试计数 = 重试计数 + 1
    返回 失败
```

### 8. 行为描述

**用途**：用精炼易懂的自然语言描述代码行为

**规则**：

- 行末出现冒号时，下一行应当增加一级缩进
- 行为描述中，非行末的冒号不会被识别为语法符号
- 行为描述中的逗号不会被识别为语法符号
- 行为描述中，不在行起始的关键字不会被识别为保留关键字
- 行为描述中，空格没有特殊含义，但合理的空格有利于大模型的代码识别转换

### 9. 人员注释 (//)

**用途**：为开发者提供注释，用于额外解释代码逻辑和意图。不会被提供给大模型

**语法模板**：

```intent_behavior_code
// 这是一行注释，不起任何作用
```

## 语法结构模板

以下是一个完整的模板示例，展示了各个语法元素的正确使用方式及其功能：

```intent_behavior_code
// 文件顶部的模块引用（可选，允许多个）
module <模块名1>[: 模块描述]
module <模块名2>[: 模块描述]

// 函数定义前的说明和注释
description: <函数对外功能描述>
@ <函数级别的意图注释>
func <函数名>(<参数1>[: 参数描述], <参数2>[: 参数描述], ...):
    // 函数体使用自然语言描述
    var <局部变量>[: 变量描述]
    <执行步骤描述>...
    $<符号引用>$...

// 类定义前的说明和注释  
description: <类的对外功能描述>
@ <类级别的意图注释>
class <类名>([父类][: 继承描述]):
    var <成员变量>[: 变量描述]
    
    description: <方法对外功能描述>
    @ <方法级别的意图注释>
    func <方法名>(<参数>[: 参数描述], ...):
        // 方法实现
        $self.成员方法的符号$(可能的参数列表)
        ...
```

## 完整示例

### 示例1：配置管理系统

```intent_behavior_code
module json: 标准JSON解析库
module threading: 线程支持库

description: 线程安全的配置管理器，支持多数据源和热重载
@ 所有公共方法都保证线程安全，使用读写锁优化性能
class ConfigManager():
    var configData: 当前配置数据
    var configPath: 主配置文件路径
    var rwLock: 读写锁对象
    
    description: 初始化配置管理器
    func __init__(配置文件路径: 字符串路径，支持相对和绝对路径):
        // 下面的变量/方法引用未写明显式引用，但已经足够明确
        self.configPath = 配置文件路径
        self.rwLock = 创建读写锁()
        self.加载配置()
    
    description: 从文件加载配置数据
    @ 使用JSON格式解析，自动处理编码问题
    func 加载配置():
        获取 self.rwLock 的写锁
        尝试:
            文件内容 = 读取文件(self.configPath)
            self.configData = json.parse(文件内容)
        捕获 异常:
            记录错误 "配置加载失败: " + 异常信息
        最后:
            释放 self.rwLock 的写锁
```

### 示例2：API客户端

```intent_behavior_code
module requests: HTTP请求库
module logging: 日志记录库

description: 
    通用的REST API客户端，封装了重试机制、
    错误处理和请求日志记录功能
class ApiClient():
    var baseUrl: API服务基础地址
    var timeout: 请求超时时间
    var session: HTTP会话对象
    
    description: 发送GET请求到指定接口
    @ 自动处理网络异常，最多重试3次
    func 获取数据(
        接口路径: 相对路径，不需要包含基础URL, 
        查询参数: 字典形式的查询参数
    ):
        完整URL = self.baseUrl + 接口路径
        重试计数 = 0
        
        当 重试计数 < 3:
            尝试:
                响应 = self.session.get(完整URL, 查询参数)
                如果 响应.状态码 == 200:
                    返回 json.parse(响应.内容)
                否则:
                    记录警告 "API返回错误: " + 响应.状态码
                    抛出异常
            捕获 网络异常:
                重试计数 = 重试计数 + 1
                等待 2的重试计数次方 秒
        
        抛出异常 "请求失败，超过最大重试次数"
```

## 编码规范

### 格式和缩进

- 统一使用4个空格进行缩进
- 参数列表可以换行对齐以提升可读性
- 冒号后内容换行时必须缩进

### 内容规范

- `description`：≤ 50汉字，多行时需要缩进
- `@`注释：≤ 30汉字，必须单行
- 参数和变量描述：≤ 50汉字，均为可选项

### 约束条件

- 禁止在函数体内使用`@`注释
- 禁止引用未定义的`$`符号
- 禁止在同一个目标前使用多个`@`注释
- 禁止在冒号后直接换行而不缩进

## 使用建议

1. **描述准确性**：用简洁准确的自然语言描述功能，避免歧义
2. **注释针对性**：使用`@`注释标注重要的设计决策和技术约束
3. **引用完整性**：确保所有`$`符号引用都是已定义的实体
4. **结构清晰性**：合理使用换行和缩进来组织复杂参数列表
5. **模块化设计**：通过`module`明确声明外部依赖关系
